plugins {
    id 'base'
    id 'eclipse'
    id 'idea'
    id 'jacoco-report-aggregation'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'org.owasp.dependencycheck' version '7.1.1'
}

// CVE vulnerability scanning
// run: ./gradlew :dependencyCheckAggregate

dependencyCheck {
    failBuildOnCVSS = 5
    suppressionFile = 'cve-suppressions.xml'
    analyzers {
        assemblyEnabled = false
    }
}

repositories {
    mavenCentral()
}

dependencies {
    jacocoAggregation project(':fahrschein'),
            project(':fahrschein-http-api'),
            project(':fahrschein-http-simple'),
            project(':fahrschein-http-apache'),
            project(':fahrschein-http-spring'),
            project(':fahrschein-http-jdk11'),
            project(':fahrschein-inmemory'),
            project(':fahrschein-metrics-dropwizard'),
            project(':fahrschein-typeresolver')
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}
